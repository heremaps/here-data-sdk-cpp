# Copyright (C) 2021 HERE Europe B.V.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0
# License-Filename: LICENSE

cmake_minimum_required(VERSION 3.9)

project(lmdb VERSION 0.9.29 LANGUAGES C)

set(CMAKE_C_STANDARD 11)
set(CMAKE_C_STANDARD_REQUIRED ON)
set(CMAKE_C_EXTENSIONS OFF)

set(LMDB_SOURCE_DIR "libraries/liblmdb")

add_library(lmdb "")

target_sources(lmdb
  PRIVATE
    "${PROJECT_SOURCE_DIR}/${LMDB_SOURCE_DIR}/lmdb.h"
    "${PROJECT_SOURCE_DIR}/${LMDB_SOURCE_DIR}/mdb.c"
    "${PROJECT_SOURCE_DIR}/${LMDB_SOURCE_DIR}/midl.c"
    "${PROJECT_SOURCE_DIR}/${LMDB_SOURCE_DIR}/midl.h"
)

target_include_directories(lmdb
  PUBLIC
    $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

include(GNUInstallDirs)
install(TARGETS lmdb
    EXPORT lmdbTargets
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
)
install(
    FILES
      "${PROJECT_SOURCE_DIR}/${LMDB_SOURCE_DIR}/lmdb.h"
      "${PROJECT_SOURCE_DIR}/${LMDB_SOURCE_DIR}/midl.h"
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/lmdb
  )
install(
    EXPORT lmdbTargets
    NAMESPACE lmdb::
    DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/lmdb"
)
install(
    FILES
        "${PROJECT_SOURCE_DIR}/cmake/lmdbConfig.cmake"
    DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/lmdb"
)