# Copyright 2015-2021 Howard Chu, Symas Corp.
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted only as authorized by the OpenLDAP
# Public License.
#
# A copy of this license is available in the file LICENSE in the
# top-level directory of the distribution or, alternatively, at
# <http://www.OpenLDAP.org/license.html>.

cmake_minimum_required(VERSION 3.9)

project(lmdb VERSION 0.9.29 LANGUAGES C)

set(CMAKE_C_STANDARD 11)
set(CMAKE_C_STANDARD_REQUIRED ON)
set(CMAKE_C_EXTENSIONS OFF)

set(LMDB_SOURCE_DIR "libraries/liblmdb")

add_library(lmdb "")

target_sources(lmdb
  PRIVATE
    "${PROJECT_SOURCE_DIR}/${LMDB_SOURCE_DIR}/lmdb.h"
    "${PROJECT_SOURCE_DIR}/${LMDB_SOURCE_DIR}/mdb.c"
    "${PROJECT_SOURCE_DIR}/${LMDB_SOURCE_DIR}/midl.c"
    "${PROJECT_SOURCE_DIR}/${LMDB_SOURCE_DIR}/midl.h"
)

target_include_directories(lmdb
  PUBLIC
    $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

include(GNUInstallDirs)
install(TARGETS lmdb
    EXPORT lmdbTargets
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
)
install(
    FILES
      "${PROJECT_SOURCE_DIR}/${LMDB_SOURCE_DIR}/lmdb.h"
      "${PROJECT_SOURCE_DIR}/${LMDB_SOURCE_DIR}/midl.h"
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/lmdb
  )
install(
    EXPORT lmdbTargets
    NAMESPACE lmdb::
    DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/lmdb"
)
install(
    FILES
        "${PROJECT_SOURCE_DIR}/cmake/lmdbConfig.cmake"
    DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/lmdb"
)